.586
DATAS SEGMENT USE16
    MESG DB 'HELLO!',0DH,0AH,'$'
    OLD1C DD ?
    ICOUNT DB 18 ;中断计数初值
    COUNT DB 10 ;显示行数控制
DATAS ENDS

CODES SEGMENT USE16
    ASSUME CS:CODES,DS:DATAS
START:
    MOV AX,DATAS
    MOV DS,AX
	CLI ;关中断
	CALL READ1C
	CALL WRITE1C
	STI ;开中断
SCAN:
	MOV AH,1
	INT 16H
	JNZ FIN
	CMP COUNT,0
	JNZ SCAN
FIN:
	CALL RESET
    MOV AH,4CH
    INT 21H
;-------------------------------------
SERVICE PROC
	PUSHA ;保护现场
	PUSH DS ;DS=40H
	MOV AX,DATAS
	MOV DS,AX ;重新给DS赋值
	DEC ICOUNT ;中断计数
	JNZ EXIT ;不满18次转
	MOV ICOUNT,18
	DEC COUNT ;显示行数减1
	MOV AH,9 ;显示字符串
	LEA DX,MESG
	INT 21H
EXIT:
	POP DS
	POPA
	IRET ;返回系统08H型中断服务子程序
SERVICE ENDP
;-------------------------------------
READ1C PROC
	MOV AX,351CH
	INT 21H
	MOV WORD PTR OLD1C,BX
	MOV WORD PTR OLD1C+2,ES
	RET
READ1C ENDP
;-------------------------------------
WRITE1C PROC
	PUSH DS
	MOV AX,CODES
	MOV DS,AX
	LEA DX,SERVICE
	MOV AX,251CH
	INT 21H
	POP DS
	RET
WRITE1C ENDP
;-------------------------------------
RESET PROC
	MOV DX,WORD PTR OLD1C
	MOV DS,WORD PTR OLD1C+2
	MOV AX,251CH
	INT 21H
	RET
RESET ENDP
CODES ENDS
    END START
